/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/

import Mathlib.Analysis.SpecialFunctions.Gamma.Beta
import Mathlib.Data.Real.Sign
import Mathlib.Analysis.PSeries
import Mathlib.NumberTheory.ZetaFunctions.SillySumLemma
/-!
# Dirichlet series as Mellin transforms

Here we prove general results of the form "the Mellin transform of a power series in exp (-t) is
a Dirichlet series".
-/

open Filter Topology Asymptotics Real Set MeasureTheory
open Complex hiding abs_of_nonneg

section sign

lemma Real.sign_eq_cast_sign (x : ‚Ñù) : sign x = SignType.sign x := by
  rcases lt_trichotomy x 0 with h | h | h <;>
  simp [h, sign_of_pos, sign_of_neg]

lemma Int.sign_eq_cast_sign (x : ‚Ñ§) : sign x = SignType.sign x := by
  rcases lt_trichotomy x 0 with h | h | h <;>
  simp [h, sign_eq_one_iff_pos, sign_eq_neg_one_iff_neg]

lemma Real.sign_mul_abs (x : ‚Ñù) : sign x * |x| = x := by
  rw [sign_eq_cast_sign, _root_.sign_mul_abs]

end sign

section Gamma‚Ñù

/-- Deligne's archimedean Gamma factor for a real infinite place, see
"Valeurs de fonctions L et periodes d'integrales" ¬ß 5.3. -/
noncomputable def Gamma‚Ñù (s : ‚ÑÇ) := œÄ ^ (-s / 2) * Complex.Gamma (s / 2)

/-- Deligne's archimedean Gamma factor for a complex infinite place, see
"Valeurs de fonctions L et periodes d'integrales" ¬ß 5.3. (Some authors omit the factor of 2). -/
noncomputable def Gamma‚ÑÇ (s : ‚ÑÇ) := 2 * (2 * œÄ) ^ (-s) * Complex.Gamma s

@[simp] lemma Gamma‚Ñù_def (s : ‚ÑÇ) : Gamma‚Ñù s = œÄ ^ (-s / 2) * Complex.Gamma (s / 2) := rfl

@[simp] lemma Gamma‚ÑÇ_def (s : ‚ÑÇ) : Gamma‚ÑÇ s = 2 * (2 * œÄ) ^ (-s) * Complex.Gamma s := rfl

lemma Gamma‚Ñù_add_two {s : ‚ÑÇ} (hs : s ‚â† 0) : Gamma‚Ñù (s + 2) = Gamma‚Ñù s * s / 2 / œÄ := by
  rw [Gamma‚Ñù, neg_div, add_div, neg_add, div_self two_ne_zero,
    Complex.Gamma_add_one _ (div_ne_zero hs two_ne_zero),
    cpow_add _ _ (ofReal_ne_zero.mpr pi_ne_zero), cpow_neg_one, Gamma‚Ñù]
  field_simp [pi_ne_zero]
  ring

lemma Gamma‚ÑÇ_add_one {s : ‚ÑÇ} (hs : s ‚â† 0) : Gamma‚ÑÇ (s + 1) = Gamma‚ÑÇ s * s / 2 / œÄ := by
  rw [Gamma‚ÑÇ, Complex.Gamma_add_one _ hs, neg_add, cpow_add _ _ (mul_ne_zero two_ne_zero
    (ofReal_ne_zero.mpr pi_ne_zero)), cpow_neg_one]
  field_simp [pi_ne_zero]
  ring

/-- Reformulation of the doubling formula (expressing compatibility of Deligne's Gamma factors
with base extensions at `‚àû`). -/
lemma Gamma‚Ñù_mul_Gamma‚Ñù_add_one (s : ‚ÑÇ) : Gamma‚Ñù s * Gamma‚Ñù (s + 1) = Gamma‚ÑÇ s := by
  simp only [Gamma‚Ñù_def, Gamma‚ÑÇ_def]
  calc
  _ = (œÄ ^ (-s / 2) * œÄ ^ (-(s + 1) / 2)) * (Gamma (s / 2) * Gamma (s / 2 + 1 / 2)) := by ring_nf
  _ = 2 ^ (1 - s) * (œÄ ^ (-1 / 2 - s) * œÄ ^ (1 / 2 : ‚ÑÇ)) * Gamma s := by
    rw [‚Üê cpow_add _ _ (ofReal_ne_zero.mpr pi_ne_zero), Complex.Gamma_mul_Gamma_add_half,
      sqrt_eq_rpow, ofReal_cpow pi_pos.le, ofReal_div, ofReal_one, ofReal_ofNat]
    ring_nf
  _ = 2 * ((2 : ‚Ñù) ^ (-s) * œÄ ^ (-s)) * Gamma s := by
    rw [sub_eq_add_neg, cpow_add _ _ two_ne_zero, cpow_one,
      ‚Üê cpow_add _ _ (ofReal_ne_zero.mpr pi_ne_zero), ofReal_ofNat]
    ring_nf
  _ = 2 * (2 * œÄ) ^ (-s) * Gamma s := by
    rw [‚Üê mul_cpow_ofReal_nonneg two_pos.le pi_pos.le, ofReal_ofNat]

/-- Reformulation of the reflection formula (expressing compatibility of Deligne Gamma factors
with Tate duality). -/
lemma Gamma‚Ñù_one_sub_mul_Gamma‚Ñù_one_add (s : ‚ÑÇ) :
    Gamma‚Ñù (1 - s) * Gamma‚Ñù (1 + s) = (cos (œÄ * s / 2))‚Åª¬π :=
  calc Gamma‚Ñù (1 - s) * Gamma‚Ñù (1 + s)
  _ = (œÄ ^ ((s - 1) / 2) * œÄ ^ ((-1 - s) / 2)) *
        (Gamma ((1 - s) / 2) * Gamma (1 - (1 - s) / 2)) := by
    simp only [Gamma‚Ñù_def]
    ring_nf
  _ = (œÄ ^ ((s - 1) / 2) * œÄ ^ ((-1 - s) / 2) * œÄ ^ (1 : ‚ÑÇ)) / sin (œÄ / 2 - œÄ * s / 2) := by
    rw [Complex.Gamma_mul_Gamma_one_sub, cpow_one]
    ring_nf
  _ = _ := by
    simp_rw [‚Üê Complex.cpow_add _ _ (ofReal_ne_zero.mpr pi_ne_zero),
      Complex.sin_pi_div_two_sub]
    ring_nf
    rw [cpow_zero, one_mul]

lemma Gamma‚Ñù_ne_zero_of_re_pos {s : ‚ÑÇ} (hs : 0 < re s) : Gamma‚Ñù s ‚â† 0 := by
  apply mul_ne_zero
  ¬∑ simp [pi_ne_zero]
  ¬∑ apply Complex.Gamma_ne_zero_of_re_pos
    rw [div_ofNat_re]
    exact div_pos hs two_pos

lemma Gamma‚Ñù_eq_zero_iff {s : ‚ÑÇ} : Gamma‚Ñù s = 0 ‚Üî ‚àÉ n : ‚Ñï, s = -(2 * n) := by
  simp [pi_ne_zero, div_eq_iff (two_ne_zero' ‚ÑÇ), mul_comm]

lemma Gamma‚Ñù_one : Gamma‚Ñù 1 = 1 := by
  rw [Gamma‚Ñù_def, Complex.Gamma_one_half_eq]
  simp [neg_div, cpow_neg, inv_mul_cancel, pi_ne_zero]

lemma Gamma‚ÑÇ_one : Gamma‚ÑÇ 1 = 1 / œÄ := by
  rw [Gamma‚ÑÇ_def, cpow_neg_one, Complex.Gamma_one]
  field_simp [pi_ne_zero]

/-- Reflection formula for `Gamma‚Ñù`. -/
lemma Gamma‚Ñù_div_Gamma‚Ñù_one_sub {s : ‚ÑÇ} (hs : ‚àÄ (n : ‚Ñï), s ‚â† -(2 * n + 1)) :
    Gamma‚Ñù s / Gamma‚Ñù (1 - s) = Gamma‚ÑÇ s * cos (œÄ * s / 2) := by
  have : Gamma‚Ñù (s + 1) ‚â† 0 := by
    simpa only [Ne.def, Gamma‚Ñù_eq_zero_iff, not_exists, ‚Üê eq_sub_iff_add_eq,
      sub_eq_add_neg, ‚Üê neg_add]
  calc Gamma‚Ñù s / Gamma‚Ñù (1 - s)
  _ = (Gamma‚Ñù s * Gamma‚Ñù (s + 1)) / (Gamma‚Ñù (1 - s) * Gamma‚Ñù (1 + s)) := by
    rw [add_comm 1 s, mul_comm (Gamma‚Ñù (1 - s)) (Gamma‚Ñù (s + 1)), ‚Üê div_div,
      mul_div_cancel _ this]
  _ = (2 * (2 * œÄ) ^ (-s) * Gamma s) / ((cos (œÄ * s / 2))‚Åª¬π) := by
    rw [Gamma‚Ñù_one_sub_mul_Gamma‚Ñù_one_add, Gamma‚Ñù_mul_Gamma‚Ñù_add_one, Gamma‚ÑÇ_def]
  _ = _ := by rw [Gamma‚ÑÇ_def, div_eq_mul_inv, inv_inv]

/-- Reformulation of reflection formula which is logically weaker, but easier to use in
functional equations for un-completed zeta functions. (Even version) -/
lemma inv_Gamma‚Ñù_one_sub {s : ‚ÑÇ} (hs : ‚àÄ (n : ‚Ñï), s ‚â† -n) :
    (Gamma‚Ñù (1 - s))‚Åª¬π = Gamma‚ÑÇ s * cos (œÄ * s / 2) * (Gamma‚Ñù s)‚Åª¬π := by
  have h1 : Gamma‚Ñù s ‚â† 0 := by
    rw [Ne.def, Gamma‚Ñù_eq_zero_iff, not_exists]
    intro n h
    specialize hs (2 * n)
    simp_all
  have h2 : ‚àÄ (n : ‚Ñï), s ‚â† -(2 * ‚Üën + 1) := by
    intro n h
    specialize hs (2 * n + 1)
    simp_all
  rw [‚Üê Gamma‚Ñù_div_Gamma‚Ñù_one_sub h2, ‚Üê div_eq_mul_inv, div_right_comm, div_self h1, one_div]

/-- Reformulation of reflection formula which is logically weaker, but easier to use in
functional equations for un-completed zeta functions. (Odd version) -/
lemma inv_Gamma‚Ñù_two_sub {s : ‚ÑÇ} (hs : ‚àÄ (n : ‚Ñï), s ‚â† -n) :
    (Gamma‚Ñù (2 - s))‚Åª¬π = Gamma‚ÑÇ s * Complex.sin (‚ÜëœÄ * s / 2) * (Gamma‚Ñù (s + 1))‚Åª¬π := by
  by_cases h : s = 1
  ¬∑ rw [h, (by ring : 2 - 1 = (1 : ‚ÑÇ)), Gamma‚Ñù_one, Gamma‚Ñù,
    neg_div, (by norm_num : (1 + 1) / 2 = (1 : ‚ÑÇ)), Complex.Gamma_one, Gamma‚ÑÇ_one,
    mul_one, Complex.sin_pi_div_two, mul_one, cpow_neg_one, mul_one, inv_inv,
    div_mul_cancel _ (ofReal_ne_zero.mpr pi_ne_zero), inv_one]
  rw [‚Üê Ne.def, ‚Üê sub_ne_zero] at h
  have h' (n : ‚Ñï) : s - 1 ‚â† -n := by
    cases' n with m
    ¬∑ rwa [Nat.cast_zero, neg_zero]
    ¬∑ rw [Ne.def, sub_eq_iff_eq_add]
      convert hs m using 2
      push_cast
      ring
  rw [(by ring : 2 - s = 1 - (s - 1)), inv_Gamma‚Ñù_one_sub h',
    (by rw [sub_add_cancel] : Gamma‚ÑÇ s = Gamma‚ÑÇ (s - 1 + 1)), Gamma‚ÑÇ_add_one h,
    (by ring : s + 1 = (s - 1) + 2), Gamma‚Ñù_add_two h, mul_sub, sub_div, mul_one,
      Complex.cos_sub_pi_div_two]
  simp_rw [mul_div_assoc, mul_inv]
  generalize (Gamma‚Ñù (s - 1))‚Åª¬π = A
  field_simp [pi_ne_zero]
  ring

lemma differentiable_Gamma‚Ñù_inv : Differentiable ‚ÑÇ (fun s ‚Ü¶ (Gamma‚Ñù s)‚Åª¬π) := by
  conv => enter [2, s]; rw [Gamma‚Ñù, mul_inv]
  refine Differentiable.mul (fun s ‚Ü¶ .inv ?_ (by simp [pi_ne_zero])) ?_
  ¬∑ refine ((differentiableAt_id.neg.div_const (2 : ‚ÑÇ)).const_cpow ?_)
    exact Or.inl (ofReal_ne_zero.mpr pi_ne_zero)
  ¬∑ exact differentiable_one_div_Gamma.comp (differentiable_id.div_const _)

lemma Gamma‚Ñù_residue_zero : Tendsto (fun s ‚Ü¶ s * Gamma‚Ñù s) (ùìù[‚â†] 0) (ùìù 2) := by
  have h : Tendsto (fun z : ‚ÑÇ ‚Ü¶ z / 2 * Gamma (z / 2)) (ùìù[‚â†] 0) (ùìù 1) := by
    refine tendsto_self_mul_Gamma_nhds_zero.comp ?_
    rw [tendsto_nhdsWithin_iff, (by simp : ùìù (0 : ‚ÑÇ) = ùìù (0 / 2))]
    exact ‚ü®(tendsto_id.div_const _).mono_left nhdsWithin_le_nhds,
      eventually_of_mem self_mem_nhdsWithin fun x hx ‚Ü¶ div_ne_zero hx two_ne_zero‚ü©
  have h' : Tendsto (fun s : ‚ÑÇ ‚Ü¶ 2 * (œÄ : ‚ÑÇ) ^ (-s / 2)) (ùìù[‚â†] 0) (ùìù 2) := by
    rw [(by simp : ùìù 2 = ùìù (2 * (œÄ : ‚ÑÇ) ^ (-(0 : ‚ÑÇ) / 2)))]
    refine Tendsto.mono_left (ContinuousAt.tendsto ?_) nhdsWithin_le_nhds
    exact continuousAt_const.mul ((continuousAt_const_cpow (ofReal_ne_zero.mpr pi_ne_zero)).comp
      (continuousAt_id.neg.div_const _))
  convert mul_one (2 : ‚ÑÇ) ‚ñ∏ (h'.mul h) using 2 with z
  rw [Gamma‚Ñù]
  ring_nf

end Gamma‚Ñù


lemma summable_int_iff_summable_nat {Œ± : Type*}
    [AddCommGroup Œ±] [UniformSpace Œ±] [UniformAddGroup Œ±] [CompleteSpace Œ±] {f : ‚Ñ§ ‚Üí Œ±} :
    Summable f ‚Üî (Summable fun (n : ‚Ñï) => f ‚Üën) ‚àß (Summable fun (n : ‚Ñï) => f (-‚Üën)) := by
  refine ‚ü®fun p ‚Ü¶ ‚ü®?_, ?_‚ü©, fun p ‚Ü¶ summable_int_of_summable_nat p.1 p.2‚ü© <;>
    apply p.comp_injective
  exacts [Nat.cast_injective, neg_injective.comp Nat.cast_injective]

lemma summable_one_div_nat_add_rpow (a : ‚Ñù) (s : ‚Ñù) :
    Summable (fun n : ‚Ñï ‚Ü¶ 1 / |n + a| ^ s) ‚Üî 1 < s := by
  suffices : ‚àÄ (b c : ‚Ñù),
      Summable (fun n : ‚Ñï ‚Ü¶ 1 / |n + b| ^ s) ‚Üí Summable (fun n : ‚Ñï ‚Ü¶ 1 / |n + c| ^ s)
  ¬∑ simp_rw [‚Üê summable_one_div_nat_rpow, Iff.intro (this a 0) (this 0 a), add_zero, Nat.abs_cast]
  refine fun b c h ‚Ü¶ summable_of_isBigO_nat h (isBigO_of_div_tendsto_nhds ?_ 1 ?_)
  ¬∑ filter_upwards [eventually_gt_atTop (Nat.ceil |b|)] with n hn hx
    have hna : 0 < n + b := by linarith [lt_of_abs_lt ((abs_neg b).symm ‚ñ∏ Nat.lt_of_ceil_lt hn)]
    exfalso
    revert hx
    positivity
  ¬∑ simp_rw [Pi.div_def, div_div, mul_one_div, one_div_div]
    refine (?_ : Tendsto (fun x : ‚Ñù ‚Ü¶ |x + b| ^ s / |x + c| ^ s) atTop (ùìù 1)).comp
      tendsto_nat_cast_atTop_atTop
    have : Tendsto (fun x : ‚Ñù ‚Ü¶ 1 + (b - c) / x) atTop (ùìù 1)
    ¬∑ simpa using tendsto_const_nhds.add ((tendsto_const_nhds (X := ‚Ñù)).div_atTop tendsto_id)
    have : Tendsto (fun x ‚Ü¶ (x + b) / (x + c)) atTop (ùìù 1)
    ¬∑ refine (this.comp (tendsto_id.atTop_add (tendsto_const_nhds (x := c)))).congr' ?_
      filter_upwards [eventually_gt_atTop (-c)] with x hx
      field_simp [(by linarith : 0 < x + c).ne']
    apply (one_rpow s ‚ñ∏ (continuousAt_rpow_const _ s (by simp)).tendsto.comp this).congr'
    filter_upwards [eventually_gt_atTop (-b), eventually_gt_atTop (-c)] with x hb hc
    rw [neg_lt_iff_pos_add] at hb hc
    rw [Function.comp_apply, div_rpow hb.le hc.le, abs_of_pos hb, abs_of_pos hc]

lemma summable_one_div_int_add_rpow (a : ‚Ñù) (s : ‚Ñù) :
    Summable (fun n : ‚Ñ§ ‚Ü¶ 1 / |n + a| ^ s) ‚Üî 1 < s := by
  simp_rw [summable_int_iff_summable_nat, ‚Üê abs_neg (‚Üë(-_ : ‚Ñ§) + a), neg_add, Int.cast_neg,
    neg_neg, Int.cast_ofNat, summable_one_div_nat_add_rpow, and_self]

variable {Œπ : Type*} [Countable Œπ]

/-- Most basic version of the "Mellin transform = Dirichlet series" argument. -/
lemma hasSum_mellin {a : Œπ ‚Üí ‚ÑÇ} {p : Œπ ‚Üí ‚Ñù} {F : ‚Ñù ‚Üí ‚ÑÇ} {s : ‚ÑÇ}
    (hp : ‚àÄ i, a i = 0 ‚à® 0 < p i) (hs : 0 < s.re)
    (hF : ‚àÄ t ‚àà Ioi 0, HasSum (fun i ‚Ü¶ a i * rexp (-p i * t)) (F t))
    (h_sum : Summable fun i ‚Ü¶ ‚Äña i‚Äñ / (p i) ^ s.re) :
    HasSum (fun i ‚Ü¶ Gamma s * a i / p i ^ s) (mellin F s) := by
  simp_rw [mellin, smul_eq_mul, ‚Üê set_integral_congr measurableSet_Ioi
    (fun t ht ‚Ü¶ congr_arg _ (hF t ht).tsum_eq), ‚Üê tsum_mul_left]
  convert hasSum_integral_of_summable_norm (Œº := volume.restrict (Ioi 0))
    (F := fun i t ‚Ü¶ t ^ (s - 1) * (a i * rexp (-p i * t))) (fun i ‚Ü¶ ?_) ?_ using 2 with i
  ¬∑ simp_rw [‚Üê mul_assoc, mul_comm _ (a _), mul_assoc (a _), mul_div_assoc, integral_mul_left]
    rcases hp i with hai | hpi
    ¬∑ rw [hai, zero_mul, zero_mul]
    have := integral_cpow_mul_exp_neg_mul_Ioi hs hpi
    simp_rw [‚Üê ofReal_mul, ‚Üê ofReal_neg, ‚Üê ofReal_exp, ‚Üê neg_mul (p i)] at this
    rw [this, one_div, inv_cpow _ _ (arg_ofReal_of_nonneg hpi.le ‚ñ∏ pi_pos.ne), div_eq_inv_mul]
  ¬∑ -- integrability of terms
    rcases hp i with hai | hpi
    ¬∑ simpa only [hai, zero_mul, mul_zero] using integrable_zero _ _ _
    simp_rw [‚Üê mul_assoc, mul_comm _ (a i), mul_assoc]
    have := Complex.GammaIntegral_convergent hs
    rw [‚Üê mul_zero (p i), ‚Üê integrableOn_Ioi_comp_mul_left_iff _ _ hpi] at this
    refine (IntegrableOn.congr_fun (this.const_mul (1 / p i ^ (s - 1)))
      (fun t (ht : 0 < t) ‚Ü¶ ?_) measurableSet_Ioi).const_mul _
    simp_rw [mul_comm (‚Üë(rexp _) : ‚ÑÇ), ‚Üê mul_assoc, neg_mul, ofReal_mul]
    rw [mul_cpow_ofReal_nonneg hpi.le ht.le, ‚Üê mul_assoc, one_div, inv_mul_cancel, one_mul]
    ¬∑ rw [Ne.def, cpow_eq_zero_iff, not_and_or]
      exact Or.inl (ofReal_ne_zero.mpr hpi.ne')
  ¬∑ -- summability of integrals of norms
    apply Summable.of_norm
    convert h_sum.mul_left (Real.Gamma s.re) using 2 with i
    simp_rw [‚Üê mul_assoc, mul_comm _ (a i), mul_assoc, norm_mul (a i), integral_mul_left]
    rw [‚Üê mul_div_assoc, mul_comm (Real.Gamma _), mul_div_assoc, norm_mul ‚Äña i‚Äñ, norm_norm]
    rcases hp i with hai | hpi
    ¬∑ simp only [hai, norm_zero, zero_mul]
    congr 1
    have := Real.integral_rpow_mul_exp_neg_mul_Ioi hs hpi
    simp_rw [‚Üê neg_mul (p i), one_div, inv_rpow hpi.le, ‚Üê div_eq_inv_mul] at this
    rw [norm_of_nonneg (integral_nonneg (fun _ ‚Ü¶ norm_nonneg _)), ‚Üê this]
    refine set_integral_congr measurableSet_Ioi (fun t ht ‚Ü¶ ?_)
    rw [norm_mul, norm_real, Real.norm_eq_abs, Real.abs_exp, Complex.norm_eq_abs,
      abs_cpow_eq_rpow_re_of_pos ht, sub_re, one_re]

/-- Shortcut version for the commonly arising special case when `p i = œÄ * q i` for some other
sequence `q`. -/
lemma hasSum_mellin_pi_mul {a : Œπ ‚Üí ‚ÑÇ} {q : Œπ ‚Üí ‚Ñù} {F : ‚Ñù ‚Üí ‚ÑÇ} {s : ‚ÑÇ}
    (hq : ‚àÄ i, a i = 0 ‚à® 0 < q i) (hs : 0 < s.re)
    (hF : ‚àÄ t ‚àà Ioi 0, HasSum (fun i ‚Ü¶ a i * rexp (-œÄ * q i * t)) (F t))
    (h_sum : Summable fun i ‚Ü¶ ‚Äña i‚Äñ / (q i) ^ s.re) :
    HasSum (fun i ‚Ü¶ œÄ ^ (-s) * Gamma s * a i / q i ^ s) (mellin F s) := by
  have hp i : a i = 0 ‚à® 0 < œÄ * q i := by rcases hq i with h | h <;> simp [h, pi_pos]
  convert hasSum_mellin hp hs (by simpa using hF) ?_ using 2 with i
  ¬∑ have : a i / ‚Üë(œÄ * q i) ^ s = œÄ ^ (-s) * a i / q i ^ s := by
      rcases hq i with h | h
      ¬∑ simp [h]
      ¬∑ rw [ofReal_mul, mul_cpow_ofReal_nonneg pi_pos.le h.le, ‚Üê div_div, cpow_neg,
          ‚Üê div_eq_inv_mul]
    simp_rw [mul_div_assoc, this]
    ring_nf
  ¬∑ have (i : Œπ) : ‚Äña i‚Äñ / ‚Üë(œÄ * q i) ^ s.re = œÄ ^ (-s.re) * ‚Äña i‚Äñ / q i ^ s.re := by
      rcases hq i with h | h
      ¬∑ simp [h]
      rw [mul_rpow pi_pos.le h.le, ‚Üê div_div, rpow_neg pi_pos.le, ‚Üê div_eq_inv_mul]
    simpa only [this, mul_div_assoc] using h_sum.mul_left _

/-- Version allowing some constant terms (which are omitted from the sums). -/
lemma hasSum_mellin_pi_mul‚ÇÄ {a : Œπ ‚Üí ‚ÑÇ} {p : Œπ ‚Üí ‚Ñù} {F : ‚Ñù ‚Üí ‚ÑÇ} {s : ‚ÑÇ}
    (hp : ‚àÄ i, 0 ‚â§ p i) (hs : 0 < s.re)
    (hF : ‚àÄ t ‚àà Ioi 0, HasSum (fun i ‚Ü¶ if p i = 0 then 0 else a i * rexp (-œÄ * p i * t)) (F t))
    (h_sum : Summable fun i ‚Ü¶ ‚Äña i‚Äñ / (p i) ^ s.re) :
    HasSum (fun i ‚Ü¶ œÄ ^ (-s) * Gamma s * a i / p i ^ s) (mellin F s) := by
  have hs' : s ‚â† 0 := fun h ‚Ü¶ lt_irrefl _ (zero_re ‚ñ∏ h ‚ñ∏ hs)
  let a' i := if p i = 0 then 0 else a i
  have hp' i : a' i = 0 ‚à® 0 < p i := by
    simp only [a']
    split_ifs with h <;> tauto
    exact Or.inr (lt_of_le_of_ne (hp i) (Ne.symm h))
  have (i t) : (if p i = 0 then 0 else a i * rexp (-œÄ * p i * t)) =
    (a' i * rexp (-œÄ * p i * t)) := by simp only [a', ite_mul, zero_mul]
  simp_rw [this] at hF
  convert hasSum_mellin_pi_mul hp' hs hF ?_ using 2 with i
  ¬∑ rcases eq_or_ne (p i) 0 with h | h <;>
    simp [h, if_false, ofReal_zero, zero_cpow hs', div_zero]
  ¬∑ refine h_sum.of_norm_bounded _ (fun i ‚Ü¶ ?_)
    simp only
    split_ifs
    ¬∑ simp only [norm_zero, zero_div]
      positivity
    ¬∑ rw [norm_of_nonneg]
      have := hp i
      positivity

/-- Tailored version for even Jacobi theta functions. -/
lemma hasSum_mellin_pi_mul_sq {a : Œπ ‚Üí ‚ÑÇ} {r : Œπ ‚Üí ‚Ñù} {F : ‚Ñù ‚Üí ‚ÑÇ} {s : ‚ÑÇ} (hs : 0 < s.re)
    (hF : ‚àÄ t ‚àà Ioi 0, HasSum (fun i ‚Ü¶ if r i = 0 then 0 else a i * rexp (-œÄ * r i ^ 2 * t)) (F t))
    (h_sum : Summable fun i ‚Ü¶ ‚Äña i‚Äñ / |r i| ^ s.re) :
    HasSum (fun i ‚Ü¶ Gamma‚Ñù s * a i / |r i| ^ s) (mellin F (s / 2)) := by
  have hs' : 0 < (s / 2).re := by rw [div_ofNat_re]; positivity
  have h (i) : r i ^ 2 = 0 ‚Üî r i = 0 := by simp
  simp_rw [‚Üê h] at hF
  have hp i : 0 ‚â§ (r i) ^ 2 := sq_nonneg _
  convert hasSum_mellin_pi_mul‚ÇÄ hp hs' hF ?_ using 3 with i
  ¬∑ rw [‚Üê neg_div, Gamma‚Ñù_def]
  ¬∑ rw [‚Üê _root_.sq_abs, ofReal_pow, ‚Üê cpow_nat_mul']
    ring_nf
    all_goals rw [arg_ofReal_of_nonneg (abs_nonneg _)]; linarith [pi_pos]
  ¬∑ convert h_sum using 3 with i
    rw [‚Üê _root_.sq_abs, ‚Üê rpow_natCast_mul (abs_nonneg _), div_ofNat_re, Nat.cast_ofNat,
      mul_div_cancel' _ two_pos.ne']

/-- Tailored version for odd Jacobi theta functions. -/
lemma hasSum_mellin_pi_mul_sq' {a : Œπ ‚Üí ‚ÑÇ} {r : Œπ ‚Üí ‚Ñù} {F : ‚Ñù ‚Üí ‚ÑÇ} {s : ‚ÑÇ} (hs : 0 < s.re)
    (hF : ‚àÄ t ‚àà Ioi 0, HasSum (fun i ‚Ü¶ a i * r i * rexp (-œÄ * r i ^ 2 * t)) (F t))
    (h_sum : Summable fun i ‚Ü¶ ‚Äña i‚Äñ / |r i| ^ s.re) :
    HasSum (fun i ‚Ü¶ Gamma‚Ñù (s + 1) * a i * Real.sign (r i) / |r i| ^ s)
    (mellin F ((s + 1) / 2)) := by
  have hs‚ÇÅ : s ‚â† 0 := fun h ‚Ü¶ lt_irrefl _ (zero_re ‚ñ∏ h ‚ñ∏ hs)
  have hs‚ÇÇ : 0 < (s + 1).re := by rw [add_re, one_re]; positivity
  have hs‚ÇÉ : s + 1 ‚â† 0 := fun h ‚Ü¶ lt_irrefl _ (zero_re ‚ñ∏ h ‚ñ∏ hs‚ÇÇ)
  have (i t) : (a i * r i * rexp (-œÄ * r i ^ 2 * t)) = if r i = 0 then 0 else
    (a i * r i * rexp (-œÄ * r i ^ 2 * t)) := by split_ifs with h <;> simp [h]
  conv at hF => enter [t, ht, 1, i]; rw [this]
  convert hasSum_mellin_pi_mul_sq hs‚ÇÇ hF ?_ using 2 with i
  ¬∑ rcases eq_or_ne (r i) 0 with h | h
    ¬∑ rw [h, abs_zero, ofReal_zero, zero_cpow hs‚ÇÅ, zero_cpow hs‚ÇÉ, div_zero, div_zero]
    ¬∑ rw [cpow_add _ _ (ofReal_ne_zero.mpr <| abs_ne_zero.mpr h), cpow_one]
      conv_rhs => enter [1]; rw [‚Üê (r i).sign_mul_abs, ofReal_mul]
      field_simp [h]
      ring_nf
  ¬∑ convert h_sum using 2 with i
    rcases eq_or_ne (r i) 0 with h | h
    ¬∑ rw [h, abs_zero, ofReal_zero, zero_rpow hs‚ÇÇ.ne', zero_rpow hs.ne', div_zero, div_zero]
    ¬∑ rw [add_re, one_re, rpow_add (abs_pos.mpr h), rpow_one, norm_mul, norm_real,
        Real.norm_eq_abs, ‚Üê div_div, div_right_comm, mul_div_cancel _ (abs_ne_zero.mpr h)]
